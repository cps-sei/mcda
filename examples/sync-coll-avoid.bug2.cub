(***************
int Lock[N];
int State[N];
1. if (State[i] == 0) {
     NEXT_XY();
     State[i] := 1;
1. } else if (State[i] == 1) {
     if (forall j . j < i -> lock[j] = 0)
       State[i] = 2;
     else
       State[i] = 1;
1. } else if(State[i] == 2) {
     if (forall j . j > i -> lock[j] = 0)
       State[i] = 3;
     else
       State[i] = 2;
1. } else if(State[i] == 3) {
     if (MOVE_TO()) {
       State[i] = 0; Lock[i] = 0;
     }
   }
****************)

type xvals = XZero | XOne | XTwo | XThree
type yvals = YZero | YOne | YTwo | YThree
type round = CheckInit | Out | In
type trans = Zero | One | Two
type state = S0 | S1 | S2 | S3
var XMAX : int
var YMAX : int
var Round : round
array InitOK[proc] : bool
array State[proc] : state
array Trans[proc] : trans
array LockX0[proc] : int
array LockY0[proc] : int
array LockXP0[proc] : int
array LockYP0[proc] : int
array LockX1[proc] : int
array LockY1[proc] : int
array LockXP1[proc] : int
array LockYP1[proc] : int
array X[proc] : int (* current X coordinate *)
array Y[proc] : int (* current Y coordinate *)
array XP[proc] : int (* next X coordinate *)
array YP[proc] : int (* next Y coordinate *)
array XF[proc] : int (* final X coordinate *)
array YF[proc] : int (* final Y coordinate *)

(* initial state *)
init (z) {
  0 < XMAX && 0 < YMAX && Round = CheckInit && 
    XMAX = 3 && YMAX = 3 &&
    InitOK[z] = False && State[z] = S0 && Trans[z] = Zero &&  
    0 <= X[z] && X[z] <= XMAX && 0 <= Y[z] && Y[z] <= YMAX && 
    0 <= XF[z] && XF[z] <= XMAX && 0 <= YF[z] && YF[z] <= YMAX && 
    XP[z] = XMAX+1 && YP[z] = YMAX+1 &&
    LockX0[z] = X[z] && LockY0[z] = Y[z] &&
    LockXP0[z] = XP[z] && LockYP0[z] = YP[z]
}

(* error state *)
unsafe (x y) {
  Round = Out && X[x] = X[y] && Y[x] = Y[y] && 
    Trans[x] = Zero && Trans[y] = Zero
}

(* check the initial condition - no two nodes must be on the same XY
   coordinate initially or finally *)
transition check_init_0 (i)
requires { Round = CheckInit && InitOK[i] = False && 
    State[i] = S0 && Trans[i] = Zero &&
    forall_other j. (State[j] <> S0 || Trans[j] <> Zero || X[i] <> X[j] || Y[i] <> Y[j]) }
{
  InitOK[i] := True;
}

(* if all nodes have successfully checked the initial condition, move
   to Round = Out *)
transition check_init_1 (i)
requires { Round = CheckInit && InitOK[i] = True &&
    State[i] = S0 && Trans[i] = Zero &&
    forall_other j. (InitOK[j] = True && State[j] = S0 && Trans[j] = Zero) }
{
  Round := Out;
}

(* a node starts entering the round -- this only happens when Round is
   Out. also copies the Lock0 variables to Lock1. *)
transition enter_round_local (i)
requires { Round = Out && Trans[i] = Zero }
{
  Trans[i] := One;
  LockX1[i] := LockX0[i];
  LockY1[i] := LockY0[i];
  LockXP1[i] := LockXP0[i];
  LockYP1[i] := LockYP0[i];
}

(* once all nodes have entered the round, Round becomes In *)
transition enter_round_global (i)
requires { Round = Out && Trans[i] = One && 
    forall_other j. (Trans[j] = One) }
{
  Round := In;
}

(* when Round is In, any node can start executing its Round()
   function, but only if no other node is also executing its Round()
   function. *)
transition start_trans (i)
requires { Round = In && Trans[i] = One && forall_other j. (Trans[j] <> Two) }
{
  Trans[i] := Two;
}

(* remain in S0 if already at (XF,YF) *)
transition trans_S0_0 (i)
requires { Round = In && Trans[i] = Two && State[i] = S0 &&
         X[i] = XF[i] && Y[i] = YF[i] }
{
  State[i] := S0; Trans[i] := Zero;
}

(* go from S0 to S1 -- case X < XF *)
transition trans_S0_1 (i)
requires { Round = In && Trans[i] = Two && State[i] = S0  && X[i] < XF[i] }
{
  State[i] := S1; Trans[i] := Zero; XP[i] := X[i] + 1;
}

(* go from S0 to S1 -- case X > XF *)
transition trans_S0_2 (i)
requires { Round = In && Trans[i] = Two && State[i] = S0  && XF[i] < X[i] }
{
  State[i] := S1; Trans[i] := Zero; XP[i] := X[i] - 1;
}

(* go from S0 to S1 -- case X = XF && Y < YF *)
transition trans_S0_3 (i)
requires { Round = In && Trans[i] = Two && State[i] = S0  && X[i] = XF[i] && Y[i] < YF[i] }
{
  State[i] := S1; Trans[i] := Zero; YP[i] := Y[i] + 1;
}

(* go from S0 to S1 -- case X = XF && Y > YF *)
transition trans_S0_4 (i)
requires { Round = In && Trans[i] = Two && State[i] = S0  && X[i] = XF[i] && YF[i] < Y[i] }
{
  State[i] := S1; Trans[i] := Zero; YP[i] := Y[i] - 1;
}

(* go from S1 to S2 if no other node has the lock or is requesting the lock *)
transition trans_01 (i)
requires { Round = In && Trans[i] = Two && State[i] = S1 && 
    forall_other j. (LockX0[j] <> XP[i] && LockXP0[j] <> XP[i] || 
                       LockX0[j] <> XP[i] && LockYP0[j] <> YP[i] || 
                       LockY0[j] <> YP[i] && LockXP0[j] <> XP[i] ||
                       LockY0[j] <> YP[i] && LockYP0[j] <> YP[i]) }
{
  State[i] := S2; Trans[i] := Zero; LockXP1[i] := XP[i]; LockYP1[i] := YP[i];
}

(* remain in S1 if a lower id node has the lock *)
transition trans_02 (i)
requires { Round = In && Trans[i] = One }
{
  State[j] := case
    | i <> j && LockX0[i] = XP[j] && LockY0[i] = YP[j] && State[j] = S1 && 
      Trans[j] = Two : S1
    | i <> j && LockXP0[i] = XP[j] && LockYP0[i] = YP[j] && State[j] = S1 && 
      Trans[j] = Two : S1
    | _ : State[j];
  Trans[j] := case
    | i <> j && LockX0[i] = XP[j] && LockY0[i] = YP[j] && State[j] = S1 &&
      Trans[j] = Two : Zero
    | i <> j && LockXP0[i] = XP[j] && LockYP0[i] = YP[j] && State[j] = S1 &&
      Trans[j] = Two : Zero
    | _ : Trans[j];
}

(* move from S2 to S3 if no higher id node requested the lock *)
transition trans_03 (i)
requires { Round = In && Trans[i] = Two && State[i] = S2 (* && 
    forall_other j. (j < i || LockXP0[j] <> XP[i] || LockYP0[j] <> YP[i]) *) }
{
  State[i] := S3; Trans[i] := Zero;
}

(* remain in S2 if a higher id node has the lock *)
(*
transition trans_04 (i)
requires { Round = In && Trans[i] = One }
{
  State[j] := case
    | j < i && LockXP0[i] = XP[j] && LockYP0[i] = YP[j] && State[j] = S2 && 
      Trans[j] = Two : S2
    | _ : State[j];
  Trans[j] := case
    | j < i && LockXP0[i] = XP[j] && LockYP0[i] = YP[j] && State[j] = S2 &&
      Trans[j] = Two : Zero
    | _ : Trans[j];
}
*)

(* go from S3 to S0 non-deterministically *)
transition trans_05 (i)
requires { Round = In && Trans[i] = Two && State[i] = S3 }
{
  State[i] := S0; Trans[i] := Zero;
  X[i] := XP[i]; Y[i] := YP[i];
  LockX1[i] := XP[i]; LockY1[i] := YP[i];
  XP[i] := XMAX+1; YP[i] := YMAX+1;
  LockXP1[i] := XMAX+1; LockYP1[i] := YMAX+1;
}

(* stay in S3 non-deterministically *)
transition trans_06 (i)
requires { Round = In && Trans[i] = Two && State[i] = S3 }
{
  State[i] := S3; Trans[i] := Zero;
}

(* If all nodes are in Zero and Round is In, this means that all nodes
   have executed their round function. In this case, Round becomes out
   and we repeat the whole process. *)
transition exit_round (i)
requires { Round = In && Trans[i] = Zero && forall_other j. Trans[j] = Zero }
{
  Round := Out;
  LockX0[j] := case
    | i = j : LockX1[j]
    | _ : LockX1[j];
  LockY0[j] := case
    | i = j : LockY1[j]
    | _ : LockY1[j];
  LockXP0[j] := case
    | i = j : LockXP1[j]
    | _ : LockXP1[j];
  LockYP0[j] := case
    | i = j : LockYP1[j]
    | _ : LockYP1[j];
}
